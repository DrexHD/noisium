//file:noinspection GroovyAccessibility
//file:noinspection GroovyAssignabilityCheck

plugins {
    id "architectury-plugin" version "${architectury_plugin_version}"
    id "dev.architectury.loom" version "${architectury_loom_version}" apply false
    id "io.github.pacifistmc.forgix" version "${forgix_plugin_version}"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    version = "${rootProject.mod_version}+mc${rootProject.supported_minecraft_version_name}"
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.

        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
            content {
                includeGroup "maven.modrinth"
            }
        }
        maven {
            name = "NeoForged"
            url = "https://maven.neoforged.net/releases/"
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// Forgix settings
// Forgix merges all modloader specific JARs into one JAR
forgix {
    group = "${rootProject.maven_group}_${rootProject.archives_base_name}"
    mergedJarName = "${mod_id}-merged-${version}.jar"
    outputDir = "build/libs/"
}

// Move the merged JAR into the build/libs/ folder, overwriting any near-empty JARs previously there
// Afterwards, delete the Merged/ folder
tasks.register("cleanRootBuildDirectory") {
    doLast {
        delete "build/"
        mkdir "build/libs/"
    }
}

// Register a task that runs Forgix' mergeJars task and then the moveMergedJar task after all subprojects have been built
tasks.register("mergeJarsAfterSubprojectsBuild") {
    dependsOn(subprojects.build)
    finalizedBy(mergeJars)
}

// Run the cleanRootBuildDirectory task before merging JARs using Forgix, to make sure GitHub Actions doesn't upload any unused build results and to prevent confusion
tasks.named("mergeJars") {
    dependsOn(cleanRootBuildDirectory)
}

// Run the clean task before building, to make sure Forgix doesn't use any JARs it shouldn't use
tasks.named("build") {
    dependsOn(clean, subprojects.clean)
    finalizedBy(mergeJarsAfterSubprojectsBuild)
}

tasks.named("assemble") {
    dependsOn(clean, subprojects.clean)
    finalizedBy(mergeJarsAfterSubprojectsBuild)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = rootProject.maven_group
            artifactId = rootProject.archivesBaseName
            from components.java
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Steveplays28/${rootProject.mod_id}")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
